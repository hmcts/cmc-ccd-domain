plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'checkstyle'
    id 'java-library'
    id 'jacoco'
    id 'com.jfrog.bintray' version "1.8.4"
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'org.springframework.boot' version '2.1.3.RELEASE' apply false
    id 'org.owasp.dependencycheck' version '3.3.2'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'org.sonarqube' version '2.6.2'
}

group = 'uk.gov.hmcts.reform.cmc'
version = '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}


repositories {
    maven { url "http://dl.bintray.com/hmcts/hmcts-maven/" }
    mavenLocal()
    jcenter()
}


dependencyManagement {
	imports {
		mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
	}
}

dependencies {
    
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.6'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.6'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.24.5'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.12.0'
    testCompile group: 'javax.el', name: 'javax.el-api', version: '3.0.0'
    testCompile group: 'org.glassfish.web', name: 'javax.el', version: '2.2.6'
    testCompile group: 'com.google.guava', name: 'guava', version: '20.0'
  
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: '3.0.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    
}


jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

checkstyle {
  maxWarnings = 50
  toolVersion = '8.14'
}


task printVersion {
    doLast {
        print project.version
    }
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/cmc-ccd-submit-domain"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'ccd-submit-domain'
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Domain associated with the money claim creation in ccd')
                root.appendNode('name', 'Money Claim CCD Domain')
                root.appendNode('url', 'https://github.com/hmcts/cmc-ccd-submit-domain')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = 'ccd-submit-domain'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/hmcts/cmc-ccd-submit-domain'
        version {
            name = project.version
        }
    }
}
